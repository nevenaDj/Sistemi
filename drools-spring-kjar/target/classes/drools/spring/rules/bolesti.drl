//created on: 15.06.2018.
package drools.spring.rules

import drools.spring.example.model.Symptom;
import drools.spring.example.model.Disease;
import drools.spring.example.model.SelectedDisease;
import drools.spring.example.model.PatientDisease;
import drools.spring.example.model.PatientCure;
import java.util.Date;

global Date before60days;
global Date before14days;
global Date before21days;
global Date before6month;

rule "Postavi broj zadovoljenih simptoma za svaku bolest"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when    
    	 $d : Disease( $symptoms : symptoms )
    	 Symptom( name memberOf $symptoms )
         accumulate ( 
         		$s : Symptom( name memberOf $symptoms );
        		$count : count($s)
        		)
    then
    	modify($d){ setNumSymptoms($count); }

end


rule "Pacijent bolovao od prehlade ili groznice u poslednjih 60 dana"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when
    	 $d : Disease(name == "sinusna infekcija", $numSymptoms : numSymptoms, specificSymptoms == 0L )
    	 $pd : PatientDisease( 
    	 	disease.name in  ("prehlada", "groznica"), 
    	 	before60days != null,
    	 	before60days.before(date) )
    	
    then
    	$d.setNumSymptoms($numSymptoms + 1);
    	$d.setSpecificSymptoms(1L);
    	update($d);
end


rule "U poslednjih 6 meseci zabeleženo barem 10 sluèajeva u kojem je pacijent imao visok pritisak"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when
    	 Number(intValue >= 10) from accumulate(
             $pd : PatientDisease( 
             	disease.name == 'visok krvni pritisak', 
             	before6month != null,
             	before6month.before(date) ),
            count($pd)
        )
    	 $d : Disease(
    	 		name == "hipertenzija", 
    	 		$numSymptoms : numSymptoms,  
    	 		numSymptoms == 0L )

    	
    then
    	modify($d){ setNumSymptoms(1L); }
end


rule "Pacijent boluje od hipertenzije više od 6 meseci"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when
    	 $d : Disease(
    	 		name == "hronicna bubrezna bolest", 
    	 		$numSymptoms : numSymptoms, 
    	 		$specificSymptoms : specificSymptoms, 
    	 		specfic1 == false  )
    	 $pd : PatientDisease( 
    	 		disease.name == "hipertenzija", 
    	 		before6month != null,
    	 		before6month.after(date) )
    	
    then
    	$d.setNumSymptoms($numSymptoms + 1);
    	$d.setSpecificSymptoms($specificSymptoms + 1);
    	$d.setSpecfic1(true);
    	update($d);
    	
end


rule "Pacijent boluje od dijabetes"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when
    	 $d : Disease(
    	 	name == "hronicna bubrezna bolest", 
    	 	$numSymptoms : numSymptoms, 
    	 	$specificSymptoms : specificSymptoms, 
    	 	specfic2 == false  )
    	 $pd : PatientDisease( disease.name == "dijabetes")
    	
    then
    	$d.setNumSymptoms($numSymptoms + 1);
    	$d.setSpecificSymptoms($specificSymptoms + 1);
    	$d.setSpecfic2(true);
    	update($d);
    	
end


rule "U poslednjih 14 dana dijagnostikovana bolest koja kao simptom ima povišenu telesnu temperaturu"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when
    	 $d : Disease(
    	 	name == "akutna bubrezna povreda", 
    	 	$numSymptoms : numSymptoms, 
    	 	$specificSymptoms : specificSymptoms, 
    	 	specfic1 == false  )
    	 PatientDisease( 
    	 	$idD : disease.id, 
    	 	before14days != null,
    	 	before14days.before(date)  )
    	 Disease(
    	 		id == $idD && 
    	 		(symptoms contains "temperatura od 40 do 41" || symptoms contains "temperatura veca od 38") )
    then
    	$d.setNumSymptoms($numSymptoms + 1);
    	$d.setSpecificSymptoms($specificSymptoms + 1);
    	$d.setSpecfic1(true);
    	update($d);
end


rule "U poslednjih 21 dan dijagnostikovana bolest za koju je primao antibiotike"
	no-loop
	lock-on-active true
	agenda-group "bolesti"
    when
    	 $d : Disease(
    	 		name == "akutna bubrezna povreda", 
    	 		$numSymptoms : numSymptoms, 
    	 		$specificSymptoms : specificSymptoms, 
    	 		specfic2 == false  )
    	 PatientDisease( 
    	 	$idD : disease.id, 
    	 	before21days != null,
    	 	before21days.before(date) )
    	 PatientCure(patientDisease.id == $idD, cure.group.name == "antibiotik")
    	
    then
    	$d.setNumSymptoms($numSymptoms + 1);
    	$d.setSpecificSymptoms($specificSymptoms + 1);
    	$d.setSpecfic2(true);
    	update($d);
end


rule "Zadovoljeni svi simptomi"
	agenda-group "pretraga bolesti"
	salience 100
	no-loop
    when
    	 $d : Disease(diseaseSymptoms.size() == numSymptoms)
    	 $s : SelectedDisease()
    then
    	
    	modify($s){ setName($d.getName()); }
end


rule "Bolest grupa 1"
	agenda-group "pretraga bolesti"
	salience 50
	no-loop
    when
    	 $d : Disease(groupe == "grupa 1", numSymptoms >= 7)
    	 $s : SelectedDisease(name == "bolest")
    then
    	modify($s){ setName($d.getName()); }

end

rule "Bolest grupa 1 - 2"
	agenda-group "pretraga bolesti"
	salience 40
	no-loop
    when
    	 $d : Disease(groupe == "grupa 1", numSymptoms >= 6)
    	 $s : SelectedDisease(name == "bolest")
    then
    	modify($s){ setName($d.getName()); }

end


rule "Bolest grupa 1 - 3"
	agenda-group "pretraga bolesti"
	salience 30
	no-loop
    when
    	 $d : Disease(groupe == "grupa 1", numSymptoms >= 5)
    	 $s : SelectedDisease(name == "bolest")
    then
    	modify($s){ setName($d.getName()); }

end

rule "Bolest grupa 1 - 4"
	agenda-group "pretraga bolesti"
	salience 20
	no-loop
    when
    	 $d : Disease(groupe == "grupa 1", numSymptoms >= 4)
    	 $s : SelectedDisease(name == "bolest")
    then
    	modify($s){ setName($d.getName()); }

end

rule "Bolest grupa 3"
	agenda-group "pretraga bolesti"
	salience 90
	no-loop
    when
    	 $d : Disease(
    	 		groupe == "grupa 3", 
    	 		numSymptoms - specificSymptoms >= 2, 
    	 		specificSymptoms >= 1)
    	 $s : SelectedDisease(name == "bolest")
    then
    	modify($s){ setName($d.getName()); }

end
